apply plugin: 'war'
apply plugin: 'jetty'

group = 'com.nablarch.report'
version = '5u12'
description = '帳票ライブラリサンプル'

buildscript {
  repositories {
    mavenLocal()
    maven { url "${developLibUrl}" }
    jcenter()
  }
  dependencies {
    classpath "com.nablarch.dev:nablarch-gradle-plugin:${nablarchGradlePluginVersion}"
    classpath "net.saliman:gradle-cobertura-plugin:2.2.3"
    classpath "io.spring.gradle:dependency-management-plugin:0.5.3.RELEASE"
  }
}

apply {
  plugin 'com.nablarch.dev.nablarch-sonarqube'
  plugin 'com.nablarch.dev.nablarch-build'
  plugin 'com.nablarch.dev.nablarch-maven-deploy'
  from 'https://github.com/nablarch/nablarch-gradle-plugin/blob/develop/scripts/cobertura/cobertura-0.0.1.gradle?raw=true'
  plugin "io.spring.dependency-management"
}

repositories {
  mavenLocal()
  maven { url "${developLibUrl}" }
  jcenter()
  
  // サードパーティ製ライブラリの格納リポジトリ
  if(project.hasProperty('nablarchExtRepoUrl')) {
       maven { url "${nablarchExtRepoUrl}" }
  }
}

dependencyManagement {
  imports {
    mavenBom 'com.nablarch.profile:nablarch-bom:5u12'
  }
  applyMavenExclusions false
}

//------------------------------------------------------------------------------
// ソースおよびリソースディレクトリの定義
//------------------------------------------------------------------------------
dependencies {
  compile 'com.nablarch.report:nablarch-report:1.1.0'
  compile 'com.nablarch.profile:nablarch-web'
  compile 'com.nablarch.framework:nablarch-common-date'
  compile 'com.nablarch.framework:nablarch-fw-batch'
  compile 'com.nablarch.framework:nablarch-common-dao'
  compile 'com.nablarch.framework:nablarch-common-code-jdbc'
  compile 'com.nablarch.framework:nablarch-common-idgenerator-jdbc'
  compile 'com.nablarch.framework:nablarch-common-auth-jdbc'
  compile 'com.nablarch.framework:nablarch-fw-messaging-http'
  compile 'net.sf.jasperreports:jasperreports:5.6.1'
  
  compile 'javax.mail:mailapi:1.4.3'
  
    
  testCompile 'com.nablarch.framework:nablarch-testing'
  
  runtime 'com.oracle:ucp:11.2.0.3.0'
  runtime 'com.oracle:ojdbc6:11.2.0.4.0'
  runtime 'com.lowagie:iTextAsian:1.0.0'
}

test {
  classpath = files("${projectDir}/src/main/java") \
       + project.sourceSets.test.runtimeClasspath
}

webAppDirName = "${projectDir}/src/main/web"
war {
  destinationDir = file("${projectDir}")
  archiveName = 'report-sample-application.war'
  from("${projectDir}/src/main/java") {
    into 'WEB-INF/classes'
    include '**/*.sql'
  }
  exclude 'tools/**'
  exclude '*.bat'
}

jettyRun {
  classpath += files("${projectDir}/src/main/java")
}

//------------------------------------------------------------------------------
// データベースのセットアップタスク
//------------------------------------------------------------------------------
if (hasProperty('db')) {
  ant.properties['db.url'] = project.db
}
if (hasProperty('databaseName')) {
  ant.properties['database.name'] = project.databaseName
}
if (hasProperty('sampleRoot')) {
  ant.properties['project.root'] = project.sampleRoot
}
if (hasProperty('sampleMain')) {
  ant.properties['project.main'] = project.sampleMain
  ant.properties['sql.lib.dir'] = project.sampleMain + '/web/WEB-INF/lib'
}
if (hasProperty('sampleTest')) {
  ant.properties['project.test'] = project.sampleTest
}
if (hasProperty('sampleTool')) {
  ant.properties['project.tool'] = project.sampleTool
}

ant.properties['project.main.classes'] = project.sourceSets.main.output.classesDir
ant.properties['project.test.classes'] = project.sourceSets.test.output.classesDir


def toolLibDir = "${buildDir}/toolLib/"

configurations {
  toolLib {
    extendsFrom testRuntime
  }
}

// ツール実行のためのライブラリを用意する。
task copyDependency(type:Copy) {
  
  // 既存のライブラリがあれば削除する。
  delete(toolLibDir)
  from (configurations.toolLib)
  into toolLibDir
}

task setupDb {
  inputs.files fileTree(dir: "${projectDir}/src/tool/db", include: '**/*')
  outputs.upToDateWhen { false }
  doLast {
    ant.ant(antfile: 'db_setup-build.xml', target: 'DB再セットアップ', inheritall: 'true', dir: project.file("${projectDir}/src/tool/db"))
  }
}
task compileJasper {
  fileTree(dir:"${projectDir}/src/tool/jrxmlcompiler", includes:['**/*'])
  doLast {
    ant.ant(antfile: 'build.xml', target:'compile',  dir:project.file("${projectDir}/src/tool/jrxmlcompiler"))
  }
}
compileJava.dependsOn compileJasper
setupDb.dependsOn compileJava, compileTestJava, copyDependency
compileJasper.dependsOn copyDependency
test.dependsOn setupDb
jettyRun.dependsOn setupDb


task wrapper(type: Wrapper) {
  gradleVersion = '2.13'
}
